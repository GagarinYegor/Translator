(* === ЛЕКСИЧЕСКИЕ ПРАВИЛА === *)

(* Основные элементы *)
$ буква = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
        | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
        | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
        | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z".

$ цифра = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9".

(* Идентификатор и метка *)
$ идентификатор = буква { буква | цифра }.

$ имя_метки = буква { буква | цифра }.
$ метка = имя_метки ":".

(* Числа *)
$ двоичное = "0" ("B" | "b") { "0" | "1" }.
$ восьмеричное = "0" { "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" }.
$ десятичное = цифра { цифра }.
$ шестнадцатеричное = "0" ("X" | "x") { цифра | "A" | "B" | "C" | "D" | "E" | "F"
                                   | "a" | "b" | "c" | "d" | "e" | "f" }.

(* Действительные числа *)
$ числовая_строка = цифра { цифра }.
$ порядок = ("E" | "e") [ "+" | "-" ] числовая_строка.
$ действительное = ( числовая_строка "." числовая_строка [ порядок ] )
                 | ( числовая_строка порядок ).

(* Общее понятие числа для синтаксиса *)
$ целое = двоичное | восьмеричное | десятичное | шестнадцатеричное.
$ число = целое | действительное.

(* === СИНТАКСИЧЕСКИЕ ПРАВИЛА === *)

(* Программа *)
$ программа = { ( описание | оператор ) ";" } конец_файла.

(* Составной оператор *)
$ составной = BST { оператор ";" } EST.

(* Описания *)
$ описание = идентификатор { "," идентификатор } ":"
             [ vector "[" целое "]" of ] тип.
$ тип = integer | real.

(* Операторы *)
$ оператор = [ метка ] непомеченный.

$ непомеченный = составной
               | присваивание
               | перехода
               | условный
               | цикла
               | пустой
               | ввода
               | вывода.

$ пустой = .

$ перехода = goto имя_метки.

$ ввода = read переменная { "," переменная }.

$ вывода = write ( выражение | спецификатор ) { "," ( выражение | спецификатор ) }.

$ переменная = идентификатор [ "[" индекс "]" ].
$ индекс = идентификатор | целое.

$ спецификатор = skip | space | tab.

(* Присваивание и выражения (инфиксная форма) *)
$ присваивание = переменная ASS выражение.

$ выражение = слагаемое { (EQ | NE | LT | GT | LE | GE) слагаемое }.
$ слагаемое = множитель { (PLUS | MIN) множитель }.
$ множитель = унарное { (MULT | DIV | MOD) унарное }.
$ унарное = [ MIN ] терм.
$ терм = переменная | число | "(" выражение ")".

(* Условный оператор *)
$ условный = if выражение then непомеченный [ else непомеченный ].

(* Оператор цикла *)
$ цикла = loop { оператор ";" } end.